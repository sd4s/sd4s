PACKAGE BODY uniconnect5 AS

L_SQLERRM         VARCHAR2(255);
L_SQL_STRING      VARCHAR2(2000);
L_WHERE_CLAUSE    VARCHAR2(1000);
L_EVENT_TP        UTEV.EV_TP%TYPE;
L_TIMED_EVENT_TP  UTEVTIMED.EV_TP%TYPE;
L_RET_CODE        NUMBER;
L_RESULT          NUMBER;
L_RETURN          INTEGER;
L_FETCHED_ROWS    NUMBER;
STPERROR          EXCEPTION;

CURSOR L_SS_CURSOR (A_SS VARCHAR2) IS
   SELECT ALLOW_MODIFY, ACTIVE
   FROM UTSS
   WHERE SS = A_SS;
L_SS_REC  L_SS_CURSOR%ROWTYPE;


FUNCTION GETVERSION
   RETURN VARCHAR2
IS
BEGIN
   RETURN('06.07.00.00_00.13');
EXCEPTION
   WHEN OTHERS THEN
      RETURN (NULL);
END GETVERSION;

FUNCTION SAVESCMECELLTABLE
(A_SC             IN     VARCHAR2,                  
 A_PG             IN     VARCHAR2,                  
 A_PGNODE         IN     NUMBER,                    
 A_PA             IN     VARCHAR2,                  
 A_PANODE         IN     NUMBER,                    
 A_ME             IN     VARCHAR2,                  
 A_MENODE         IN     NUMBER,                    
 A_REANALYSIS     IN     NUMBER,                    
 A_CELL           IN     UNAPIGEN.VC20_TABLE_TYPE,  
 A_INDEX_X        IN     UNAPIGEN.NUM_TABLE_TYPE,   
 A_INDEX_Y        IN     UNAPIGEN.NUM_TABLE_TYPE,   
 A_VALUE_F        IN     UNAPIGEN.FLOAT_TABLE_TYPE, 
 A_VALUE_S        IN     UNAPIGEN.VC40_TABLE_TYPE,  
 A_SELECTED       IN     UNAPIGEN.CHAR1_TABLE_TYPE, 
 A_MODIFY_FLAG    IN OUT UNAPIGEN.NUM_TABLE_TYPE,   
 A_NR_OF_ROWS     IN     NUMBER)                    

RETURN NUMBER IS

L_MT_VERSION             VARCHAR2(20);
L_LC                     VARCHAR2(2);
L_LC_VERSION             VARCHAR2(20);
L_SS                     VARCHAR2(2);
L_LOG_HS                 CHAR(1);
L_LOG_HS_DETAILS         CHAR(1);
L_ALLOW_MODIFY           CHAR(1);
L_ACTIVE                 CHAR(1);
L_ME_EXEC_END_DATE       TIMESTAMP WITH TIME ZONE;
L_PA_EXEC_END_DATE       TIMESTAMP WITH TIME ZONE;
L_ME_REANALYSIS          INTEGER;
L_PA_REANALYSIS          INTEGER;
L_WHAT_DESCRIPTION       VARCHAR2(255);
L_HS_SEQ                 INTEGER;
L_CNT                    INTEGER;
L_EV_SEQ_NR              NUMBER;

CURSOR L_ME_EXEC_END_DATE_CURSOR (A_SC VARCHAR2,
                        A_PG VARCHAR2, A_PGNODE NUMBER,
                        A_PA VARCHAR2, A_PANODE NUMBER,
                        A_ME VARCHAR2, A_MENODE NUMBER) IS
   SELECT EXEC_END_DATE, REANALYSIS
   FROM UTSCME
   WHERE SC = A_SC
   AND PG = A_PG
   AND PGNODE = A_PGNODE
   AND PA = A_PA
   AND PANODE = A_PANODE
   AND ME = A_ME
   AND MENODE = A_MENODE;

CURSOR L_PA_EXEC_END_DATE_CURSOR (A_SC VARCHAR2,
                        A_PG VARCHAR2, A_PGNODE NUMBER,
                        A_PA VARCHAR2, A_PANODE NUMBER) IS
   SELECT EXEC_END_DATE
   FROM UTSCPA
   WHERE SC = A_SC
   AND PG = A_PG
   AND PGNODE = A_PGNODE
   AND PA = A_PA
   AND PANODE = A_PANODE;

CURSOR L_SCMECELLLISTOLD_CURSOR (A_SC IN VARCHAR2,   A_PG IN VARCHAR2, 
                                 A_PGNODE IN NUMBER, A_PA IN VARCHAR2, 
                                 A_PANODE IN NUMBER, A_ME IN VARCHAR2, 
                                 A_MENODE IN NUMBER, A_CELL IN VARCHAR2,
                                 A_INDEX_X IN NUMBER, A_INDEX_Y IN NUMBER) IS
   SELECT *
   FROM UTSCMECELLLIST A
   WHERE A.SC = A_SC
     AND A.PG = A_PG
     AND A.PGNODE = A_PGNODE
     AND A.PA = A_PA
     AND A.PANODE = A_PANODE
     AND A.ME = A_ME
     AND A.MENODE = -A_MENODE
     AND A.CELL = A_CELL
     AND A.INDEX_X = A_INDEX_X
     AND A.INDEX_Y = A_INDEX_Y;
L_SCMECELLLISTOLD_REC UTSCMECELLLIST%ROWTYPE;
L_SCMECELLLISTNEW_REC UTSCMECELLLIST%ROWTYPE;

BEGIN

   IF UNAPIGEN.BEGINTXN(UNAPIGEN.P_SINGLE_API_TXN) <>
      UNAPIGEN.DBERR_SUCCESS THEN
      RAISE STPERROR;
   END IF;

   IF NVL(A_SC, ' ') = ' ' OR
      NVL(A_PG, ' ') = ' ' OR
      NVL(A_PGNODE, 0) = 0 OR
      NVL(A_PA, ' ') = ' ' OR
      NVL(A_PANODE, 0) = 0 OR
      NVL(A_ME, ' ') = ' ' OR
      NVL(A_MENODE, 0) = 0 OR
      A_REANALYSIS IS NULL THEN
      UNAPIGEN.P_TXN_ERROR := UNAPIGEN.DBERR_NOOBJID;
      RAISE STPERROR;
   END IF;

   L_SQLERRM := NULL;
     
   L_RET_CODE := UNAPIAUT.GETSCMEAUTHORISATION(A_SC, A_PG, A_PGNODE, A_PA, A_PANODE, A_ME, 
                                               A_MENODE, A_REANALYSIS, L_MT_VERSION, L_LC, 
                                               L_LC_VERSION, L_SS, L_ALLOW_MODIFY, L_ACTIVE, 
                                               L_LOG_HS, L_LOG_HS_DETAILS);
   IF L_RET_CODE <> UNAPIGEN.DBERR_SUCCESS THEN
      UNAPIGEN.P_TXN_ERROR := L_RET_CODE;
      IF UNAPIAUT.P_NOT_AUTHORISED IS NOT NULL THEN
         L_SQLERRM := UNAPIAUT.P_NOT_AUTHORISED;
      END IF;
      RAISE STPERROR;
   END IF;

   
   
   
   OPEN L_ME_EXEC_END_DATE_CURSOR (A_SC, A_PG, A_PGNODE,
                                   A_PA, A_PANODE, A_ME, A_MENODE);
   FETCH L_ME_EXEC_END_DATE_CURSOR INTO L_ME_EXEC_END_DATE, L_ME_REANALYSIS;
   IF L_ME_EXEC_END_DATE_CURSOR%NOTFOUND THEN
      UNAPIGEN.P_TXN_ERROR := UNAPIGEN.DBERR_NOOBJECT;
      CLOSE L_ME_EXEC_END_DATE_CURSOR;
      RAISE STPERROR;
   END IF;
   CLOSE L_ME_EXEC_END_DATE_CURSOR;
   
   IF L_ME_EXEC_END_DATE IS NOT NULL THEN
      L_RET_CODE := UNAPIMEP.REANALSCMEFROMDETAILS
                    (A_SC, A_PG, A_PGNODE,
                     A_PA, A_PANODE,
                     A_ME, A_MENODE, L_ME_REANALYSIS, 'SaveScMeCellTable');
      IF L_RET_CODE <> UNAPIGEN.DBERR_SUCCESS THEN
         UNAPIGEN.P_TXN_ERROR := L_RET_CODE;
         RAISE STPERROR;
      END IF;
   END IF;

   
   
   
   
   OPEN L_PA_EXEC_END_DATE_CURSOR (A_SC, 
                                   A_PG, A_PGNODE,
                                   A_PA, A_PANODE);
   FETCH L_PA_EXEC_END_DATE_CURSOR INTO L_PA_EXEC_END_DATE;
   IF L_PA_EXEC_END_DATE_CURSOR%NOTFOUND THEN
      UNAPIGEN.P_TXN_ERROR := UNAPIGEN.DBERR_NOOBJECT;
      CLOSE L_PA_EXEC_END_DATE_CURSOR;
      RAISE STPERROR;
   END IF;
   CLOSE L_PA_EXEC_END_DATE_CURSOR;
   
   IF L_PA_EXEC_END_DATE IS NOT NULL THEN
      L_RET_CODE := UNAPIMEP.REANALSCPAFROMDETAILS
                    (A_SC, A_PG, A_PGNODE,
                     A_PA, A_PANODE,
                     L_PA_REANALYSIS, 'SaveScMeCellValues');
      IF L_RET_CODE <> UNAPIGEN.DBERR_SUCCESS THEN
         UNAPIGEN.P_TXN_ERROR := L_RET_CODE;
         RAISE STPERROR;
      END IF;
   END IF;

   
   FOR L_SEQ_NO IN 1..A_NR_OF_ROWS LOOP
      UPDATE UTSCMECELLLIST
      SET MENODE = -MENODE
      WHERE SC = A_SC
      AND PG = A_PG
      AND PGNODE = A_PGNODE
      AND PA = A_PA
      AND PANODE = A_PANODE
      AND ME = A_ME
      AND MENODE = A_MENODE
      AND CELL = A_CELL(L_SEQ_NO)
      AND INDEX_X = A_INDEX_X(L_SEQ_NO)
      AND INDEX_Y = A_INDEX_Y(L_SEQ_NO);
   END LOOP;
   
   IF L_LOG_HS_DETAILS = '1' THEN
      L_RET_CODE := UNAPIGEN.GETNEXTEVENTSEQNR(L_EV_SEQ_NR);
      IF L_RET_CODE <> 0 THEN
         UNAPIGEN.P_TXN_ERROR := L_RET_CODE;
         RAISE STPERROR;
      END IF;
   END IF;
   
   L_HS_SEQ := 0;      
   FOR L_SEQ_NO IN 1..A_NR_OF_ROWS LOOP

      IF NVL(A_SELECTED(L_SEQ_NO), ' ') NOT IN ('1','0') THEN
         UNAPIGEN.P_TXN_ERROR := UNAPIGEN.DBERR_SELECTED;
         RAISE STPERROR;
      END IF;
      
      IF NVL(A_MODIFY_FLAG(L_SEQ_NO), UNAPIGEN.DBERR_SUCCESS) NOT IN
                                    (UNAPIGEN.MOD_FLAG_INSERT,
                                     UNAPIGEN.MOD_FLAG_UPDATE,
                                     UNAPIGEN.MOD_FLAG_DELETE,
                                     UNAPIGEN.DBERR_SUCCESS) THEN
         UNAPIGEN.P_TXN_ERROR := UNAPIGEN.DBERR_INVALMODFLAG;
         RAISE STPERROR;         
      END IF;
      
      
      IF A_MODIFY_FLAG(L_SEQ_NO) = UNAPIGEN.MOD_FLAG_INSERT THEN

         BEGIN
            INSERT INTO UTSCMECELLLIST(SC, PG, PGNODE, PA, PANODE, ME, MENODE, REANALYSIS,
                                       CELL, INDEX_X, INDEX_Y, VALUE_F, VALUE_S,
                                       SELECTED)
            VALUES(A_SC, A_PG, A_PGNODE, A_PA, A_PANODE, A_ME, A_MENODE, L_ME_REANALYSIS,
                 A_CELL(L_SEQ_NO), A_INDEX_X(L_SEQ_NO), A_INDEX_Y(L_SEQ_NO),
                 A_VALUE_F(L_SEQ_NO), A_VALUE_S(L_SEQ_NO), A_SELECTED(L_SEQ_NO));
         EXCEPTION
         WHEN DUP_VAL_ON_INDEX THEN
            UPDATE UTSCMECELLLIST
            SET VALUE_F=A_VALUE_F(L_SEQ_NO),
                VALUE_S=A_VALUE_S(L_SEQ_NO),
                SELECTED=A_SELECTED(L_SEQ_NO)
            WHERE SC=A_SC
              AND PG=A_PG
              AND PGNODE=A_PGNODE
              AND PA=A_PA
              AND PANODE=A_PANODE
              AND ME=A_ME
              AND MENODE=A_MENODE
              AND CELL=A_CELL(L_SEQ_NO)
              AND INDEX_X=A_INDEX_X(L_SEQ_NO)
              AND INDEX_Y=A_INDEX_Y(L_SEQ_NO);
            IF SQL%ROWCOUNT=0 THEN
               UNAPIGEN.P_TXN_LEVEL := UNAPIGEN.DBERR_NOOBJECT;
               RAISE STPERROR;
            END IF;
         END;
         L_SCMECELLLISTNEW_REC.VALUE_F := A_VALUE_F(L_SEQ_NO);
         L_SCMECELLLISTNEW_REC.VALUE_S := A_VALUE_S(L_SEQ_NO);
         L_SCMECELLLISTNEW_REC.SELECTED := A_SELECTED(L_SEQ_NO);

      ELSIF A_MODIFY_FLAG(L_SEQ_NO) = UNAPIGEN.MOD_FLAG_UPDATE THEN
         UPDATE UTSCMECELLLIST
         SET VALUE_F=A_VALUE_F(L_SEQ_NO),
             VALUE_S=A_VALUE_S(L_SEQ_NO),
             SELECTED=A_SELECTED(L_SEQ_NO)
         WHERE SC=A_SC
           AND PG=A_PG
           AND PGNODE=A_PGNODE
           AND PA=A_PA
           AND PANODE=A_PANODE
           AND ME=A_ME
           AND MENODE=A_MENODE
           AND CELL=A_CELL(L_SEQ_NO)
           AND INDEX_X=A_INDEX_X(L_SEQ_NO)
           AND INDEX_Y=A_INDEX_Y(L_SEQ_NO);
         IF SQL%ROWCOUNT=0 THEN
            UNAPIGEN.P_TXN_LEVEL := UNAPIGEN.DBERR_NOOBJECT;
            RAISE STPERROR;
         END IF;
         L_SCMECELLLISTNEW_REC.VALUE_F := A_VALUE_F(L_SEQ_NO);
         L_SCMECELLLISTNEW_REC.VALUE_S := A_VALUE_S(L_SEQ_NO);
         L_SCMECELLLISTNEW_REC.SELECTED := A_SELECTED(L_SEQ_NO);

      ELSIF A_MODIFY_FLAG(L_SEQ_NO) = UNAPIGEN.MOD_FLAG_DELETE THEN
         DELETE FROM UTSCMECELLLIST
         WHERE SC=A_SC
           AND PG=A_PG
           AND PGNODE=A_PGNODE
           AND PA=A_PA
           AND PANODE=A_PANODE
           AND ME=A_ME
           AND MENODE=A_MENODE
           AND CELL=A_CELL(L_SEQ_NO)
           AND INDEX_X=A_INDEX_X(L_SEQ_NO)
           AND INDEX_Y=A_INDEX_Y(L_SEQ_NO);
         IF SQL%ROWCOUNT=0 THEN
            UNAPIGEN.P_TXN_LEVEL := UNAPIGEN.DBERR_NOOBJECT;
            RAISE STPERROR;
         END IF;
         L_SCMECELLLISTNEW_REC.VALUE_F := NULL;
         L_SCMECELLLISTNEW_REC.VALUE_S := NULL;
         L_SCMECELLLISTNEW_REC.SELECTED := NULL;
      END IF;
      IF SQL%ROWCOUNT>0 AND 
         L_LOG_HS_DETAILS = '1' THEN
         L_SCMECELLLISTOLD_REC := NULL;
         OPEN L_SCMECELLLISTOLD_CURSOR(A_SC, A_PG, A_PGNODE, A_PA, A_PANODE, A_ME, A_MENODE,
                                       A_CELL(L_SEQ_NO), A_INDEX_X(L_SEQ_NO), A_INDEX_Y(L_SEQ_NO));
         FETCH L_SCMECELLLISTOLD_CURSOR
         INTO L_SCMECELLLISTOLD_REC;
         IF L_SCMECELLLISTOLD_CURSOR%NOTFOUND THEN
            L_SCMECELLLISTOLD_REC.SC := A_SC;
            L_SCMECELLLISTOLD_REC.PG := A_PG;
            L_SCMECELLLISTOLD_REC.PGNODE := A_PGNODE;
            L_SCMECELLLISTOLD_REC.PA := A_PA;
            L_SCMECELLLISTOLD_REC.PANODE := A_PANODE;
            L_SCMECELLLISTOLD_REC.ME := A_ME;
            L_SCMECELLLISTOLD_REC.MENODE := A_MENODE;
            L_SCMECELLLISTOLD_REC.REANALYSIS := L_ME_REANALYSIS;
            L_SCMECELLLISTOLD_REC.CELL:= A_CELL(L_SEQ_NO);
            L_SCMECELLLISTOLD_REC.INDEX_X := A_INDEX_X(L_SEQ_NO);
            L_SCMECELLLISTOLD_REC.INDEX_Y := A_INDEX_Y(L_SEQ_NO);
            L_SCMECELLLISTOLD_REC.SELECTED := '0';
         ELSE
            L_SCMECELLLISTOLD_REC.MENODE := -L_SCMECELLLISTOLD_REC.MENODE;
         END IF;
         CLOSE L_SCMECELLLISTOLD_CURSOR;

         L_SCMECELLLISTNEW_REC.SC := A_SC;
         L_SCMECELLLISTNEW_REC.PG := A_PG;
         L_SCMECELLLISTNEW_REC.PGNODE := A_PGNODE;
         L_SCMECELLLISTNEW_REC.PA := A_PA;
         L_SCMECELLLISTNEW_REC.PANODE := A_PANODE;
         L_SCMECELLLISTNEW_REC.ME := A_ME;
         L_SCMECELLLISTNEW_REC.MENODE := A_MENODE;
         L_SCMECELLLISTNEW_REC.REANALYSIS := L_ME_REANALYSIS;
         L_SCMECELLLISTNEW_REC.CELL:= A_CELL(L_SEQ_NO);
         L_SCMECELLLISTNEW_REC.INDEX_X := A_INDEX_X(L_SEQ_NO);
         L_SCMECELLLISTNEW_REC.INDEX_Y := A_INDEX_Y(L_SEQ_NO);

         UNAPIHSDETAILS.ADDSCMECELLLISTHSDETAILS(L_SCMECELLLISTOLD_REC, L_SCMECELLLISTNEW_REC,
                                                 UNAPIGEN.P_TR_SEQ, L_EV_SEQ_NR,L_HS_SEQ);                                                 
      END IF;      
   END LOOP;

   DELETE FROM UTSCMECELLLIST
   WHERE SC = A_SC
   AND PG = A_PG
   AND PGNODE = A_PGNODE
   AND PA = A_PA
   AND PANODE = A_PANODE
   AND ME = A_ME
   AND MENODE = -A_MENODE;

   IF UNAPIGEN.ENDTXN <> UNAPIGEN.DBERR_SUCCESS THEN
      RAISE STPERROR;
   END IF;
   
   RETURN(UNAPIGEN.DBERR_SUCCESS);

EXCEPTION
WHEN OTHERS THEN
   IF SQLCODE <> 1 THEN
      UNAPIGEN.LOGERROR('SaveScMeCellTable',SQLERRM);
   ELSIF L_SQLERRM IS NOT NULL THEN
      UNAPIGEN.LOGERROR('SaveScMeCellTable',L_SQLERRM);   
   END IF;
   RETURN(UNAPIGEN.ABORTTXN(UNAPIGEN.P_TXN_ERROR, 'SaveScMeCellTable'));
END SAVESCMECELLTABLE;

END UNICONNECT5;