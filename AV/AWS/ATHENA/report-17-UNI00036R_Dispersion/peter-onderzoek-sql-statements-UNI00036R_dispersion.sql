--* File UNI00036R_dispersion.fex


-SET &TODAY = EDIT(&YYMD, '9999/99/99 00:00:00');
 
-DEFAULT &PART_NO			= 'NONE';
-DEFAULT &PART_NO0			= 1;
-DEFAULT &EXPLOSION_DATE	= &TODAY.EVAL;
-DEFAULT &BOM_TYPE			= DISPERSION;
-DEFAULT &IMPORT_ID			= '';
 
-DEFAULT &WFFMT				= HTML;
-DEFAULT &TPFMT				= 'D12.3SC';
-DEFAULT &ALLOW_DEV			= 0;
-DEFAULTH &_PART_NO			= FOC_NONE;
-DEFAULTH &_PART_NO0		= 1;
-SET &PREFERRED		= 1;
 
-SET &PART_NO = IF &_PART_NO NE FOC_NONE THEN &_PART_NO ELSE &PART_NO;
-SET &PART_NO0 = IF &_PART_NO NE FOC_NONE THEN &_PART_NO0 ELSE &PART_NO0;
-SET &_EXPLOSION_DATE = &EXPLOSION_DATE;
 
-SET &LIMIT_ON = IF &ALLOW_DEV GT 0 THEN 'HIGHEST' ELSE 'LOWEST';
 
-*-SET &ECHO = ON;
-*-SET &ECHO_BOM = ON;
 
-*$ Standard tree explosion
-SET &BOM_TREE	= IF &BOM_TYPE EQ 'TREE' THEN '' ELSE '-*';
-*$ Raw materials only (summed)
-SET &BOM_RAW		= IF &BOM_TYPE EQ 'RAW_MATERIALS' THEN '' ELSE '-*';
 
-* Footer
-SET &FOOTERTYPE = 'LINE';
-SET &REPORTNAME = &FOCFEXNAME;
-SET &REPORTREVISION = '$Revision: 7f418736956d $';
-SET &REPORTOWNER = 'Benthem, H. van';
-*-SET &REPORTWIKI = 'XXX';
-SET &FORMAT = IF &BOM_TYPE EQ 'DISPERSION' THEN XLSX ELSE &WFFMT;
-INCLUDE GEN00003_rtvFooter
 
-SET &REPORTCODE = 'UNI00036';
 
-INCLUDE UNI00020P_initauth
-INCLUDE UNI00020P_Config
 
 
SET ASNAMES = ON
SET HOLDLIST = PRINTONLY
SET ERROROUT = OFF
 
SET CDN = SPACE
SET ACROSSTITLE = SIDE
 
SET EXCELSERVURL = ''
-RUN
 
-INCLUDE UNI00036P_Config
 
-* Initialization for Excel template
-SET &TEMPLATE_IN	= 'unilab/sheets/' || &TEMPLATE || &TEMPLATE_EXT;
-SET &TEMPLATE_OUT	= 'UNI00036H1';
 
 
 
-*SET XRETRIEVAL = OFF
 
-INCLUDE UNI00020P_bundleParts
-*** END UNI00020P_bundleParts
 
-INCLUDE UNI00020R_partBOM_explosion
-*** END UNI00020R_partBOM_explosion
 
-IF &BOM_TYPE NE 'DISPERSION' THEN GOTO :FULLBOM;
 
-INCLUDE GEN00031_xlsx_sheetInit
 
DEFINE FILE UNI00016H_EXPLODED
	IMPORTID/A20		= '&IMPORT_ID';
	PRD_DATE/HYYMDS		= DT('&_EXPLOSION_DATE');
	VOLPCT/D12.2SC% MISSING ON SOME = IF SPEC_TYPE EQ 'FILL' OR 'TCE_FILL' THEN 100 * INGREDIENT_VOLUME ELSE MISSING;
END
TABLE FILE UNI00016H_EXPLODED
SUM
	VOLPCT	AS 'Fi'
 
BY ROOT_PART	AS 'Compound'
BY IMPORTID		AS 'Import ID'
BY PRD_DATE		AS 'Production date'
 
-INCLUDE GEN00005_rtvFooting
ENDSTYLE
END
-RUN
 
-SET &BOM_TYPE = 'TREE';
-SET &INDENTSTR = HEXBYT(160, 'A1');
 
-INCLUDE UNI00020R_partBOM_explosion
 
-INCLUDE GEN00030_xlsx_nextSheet
-SET &TEMPLATE_OUT = '';
-INCLUDE GEN00031_xlsx_sheetInit
 
DEFINE FILE UNI00016H_EXPLODED
	VOLPCT/D12.2SC% MISSING ON SOME = 100 * INGREDIENT_VOLUME;
	PN/A90V = INDENTSTR || INDENTSTR || INDENTSTR || INDENTSTR || PART_NO;
END
TABLE FILE UNI00016H_EXPLODED
SUM
	PART_DESCRIPTION	AS 'Description'
	VOLPCT				AS 'Vol%'
BY ROOT_PART			NOPRINT
BY ROOT_REV				NOPRINT
BY ROOT_DESCRIPTION		NOPRINT
 
BY ALTERNATIVE			NOPRINT
BY PLANT				NOPRINT
 
BY BRANCH				NOPRINT
BY PARENT_BRANCH		NOPRINT
BY LVL					NOPRINT
 
BY PN					AS 'Component'
 
ON ROOT_PART SUBHEAD
"Exploded part<+0>: <ROOT_PART"
 
ON ALTERNATIVE SUBHEAD
"Alternative<+0>: <ALTERNATIVE - Plant<+0>: <PLANT"
 
ON TABLE NOTOTAL
ON TABLE SET EMPTYREPORT ON
 
-INCLUDE GEN00005_rtvFooting
 
TYPE=SUBHEAD, BY=ROOT_PART, CLASS=SUBHEAD emph,$
TYPE=SUBHEAD, BY=SIMILARROW, CLASS=SUBHEAD,$
 
TYPE=HEADING, ITEM=1, CLASS=HEADING, WIDTH=2.7,$
TYPE=HEADING, ITEM=2, CLASS=HEADING, WIDTH=0.3,$
TYPE=HEADING, ITEM=3, CLASS=HEADING, WIDTH=5.0,$
 
ENDSTYLE
END
-RUN
 
-GOTO :BOMEND
 
-:FULLBOM
 
-***
-* BoM explosion code with Vol% per BoM-item
-***
DEFINE FILE UNI00016H_EXPLODED
	ROOT_REV2/I10SC	= ROOT_REV;
 
	VOLPCT/D12.2SC% MISSING ON SOME = IF SPEC_TYPE EQ 'FILL' THEN 100 * INGREDIENT_VOLUME ELSE MISSING;
END
TABLE FILE UNI00016H_EXPLODED
PRINT
	COMPUTE ROOT_REV/I10 = ROOT_REV;	NOPRINT
 
&BOM_TREE.EVAL	COMPUTE TREEPART_NO/A160V = INDENTSTR || INDENTSTR || PART_NO; AS 'Part-no.'
	REVISION							AS 'Rev.'
	PART_DESCRIPTION					AS 'Description'
 
	PHR									AS 'PHR,'
	DENSITY								AS 'Density,(kg/l)'
	QUANTITY							AS 'Quantity,'
	UOM									AS ''
 
	INGREDIENT_VOLUME					AS 'Vol,(l)'
	VOLPCT								AS 'Vol%,'
 
	SPEC_TYPE							AS 'Specification,type'	NOPRINT
 
	COMPUTE CHILDPATH/A292V = IF (LAST PATH EQ '')
				 OR (LAST ROOT_PART NE ROOT_PART)
				 OR (LAST ALTERNATIVE NE ALTERNATIVE)
			THEN PATH
			ELSE IF (LAST PATH GT PATH) THEN LAST PATH
			ELSE LAST CHILDPATH;
		NOPRINT
 
	COMPUTE IS_LEAF/I3 = IF (LAST ROOT_PART NE ROOT_PART)
			  OR (LAST ALTERNATIVE NE ALTERNATIVE)
			  OR (PATH || ':' || PATHNODE NE CHILDPATH)
			THEN 1
			ELSE 0;
		NOPRINT
 
	PARENT_PART								NOPRINT
	ISSUED_DATE								NOPRINT
	E_CYCLIC								NOPRINT
	E_ISSUED								NOPRINT
 
&BOM_RAW.EVAL LVL							NOPRINT
&BOM_RAW.EVAL ITEM_NUMBER					NOPRINT
 
BY ROOT_PART								NOPRINT
BY ROOT_REV									NOPRINT
BY ROOT_DESCRIPTION							NOPRINT
 
-* Tree layout data
&BOM_TREE.EVAL BY HIGHEST PREFERRED		NOPRINT
BY PREFERRED								NOPRINT
BY ALTERNATIVE								NOPRINT
BY FULLPATH									NOPRINT
BY PATH										NOPRINT
BY PATHNODE									NOPRINT
 
&BOM_TREE.EVAL BY ITEM_NUMBER			NOPRINT
&BOM_TREE.EVAL BY LVL					NOPRINT
 
BY SPEC_TYPE								NOPRINT
 
-* Raw materials layout data
&BOM_RAW.EVAL BY PART_NO					AS 'Part-no.'
BY &LIMIT_ON 1 REVISION						NOPRINT
&BOM_RAW.EVAL WHERE TOTAL IS_LEAF EQ 1;
 
ON TABLE SUBHEAD
"Bill of Materials and filler dispersion"
 
ON TABLE SUBTOTAL PHR QUANTITY INGREDIENT_VOLUME VOLPCT AS 'Total'
 
HEADING
"Part-no<+0>: <ROOT_PART ;<+0> Rev<+0>: <ROOT_REV2"
"Description<+0>: <ROOT_DESCRIPTION"
"Alternative<+0>: <ALTERNATIVE <+0> Preferred<+0>: <PREFERRED"
"Production date<+0>: <+0>&_EXPLOSION_DATE"
"Import ID<+0>: <+0>&IMPORT_ID"
 
-INCLUDE GEN00005_rtvFooting
 
TYPE=HEADING, ITEM=1, CLASS=HEADING, WIDTH=3.2,$
TYPE=HEADING, ITEM=2, CLASS=HEADING, WIDTH=0.3,$
TYPE=HEADING, ITEM=4, CLASS=HEADING, WIDTH=0.3,$
TYPE=HEADING, ITEM=5, CLASS=HEADING, WIDTH=2,$
TYPE=HEADING, ITEM=6, CLASS=HEADING, WIDTH=0.3,$
 
TYPE=HEADING, ITEM=3, LINE=1, CLASS=HEADING, WIDTH=3,$
TYPE=HEADING, ITEM=3, LINE=3, CLASS=HEADING, WIDTH=3,$
 
TYPE=HEADING, LINE=5, ITEM=3, CLASS=HEADING, WIDTH=3,
	FOCEXEC=&FOCFEXNAME(
		_PART_NO=&_PART_NO.QUOTEDSTRING
		_EXPLOSION_DATE=&_EXPLOSION_DATE.QUOTEDSTRING
		IMPORT_ID=&IMPORT_ID.QUOTEDSTRING
	),
$
ENDSTYLE
END
-RUN
 
-:BOMEND
-INCLUDE GEN00006_rtvNoResults
-INCLUDE GEN00003_rtvFooter
